# Iteration 2: formal competency questions


## Question 1

What are the current roles of all the agents?

    PREFIX allot: <https://w3id.org/akn/ontology/allot/>
    PREFIX undo: <https://w3id.org/akn/ontology/undo/>
    PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
    PREFIX tvc: <http://www.essepuntato.it/2012/04/tvc/>
    
    SELECT DISTINCT ?agent ?role ?context
    WHERE {
        ?agent a undo:Agent ;
            tvc:hasValue ?value .
            
        ?value a undo:ValueInTimeAndContext ; 
            tvc:withValue ?role ;
            tvc:withinContext ?context .
        
        FILTER NOT EXISTS {
            ?value tvc:atTime/ti:hasIntervalEndDate ?time
        }
            
        ?role a allot:Role .
    }


## Question 2

What roles have been held by Sergio Mattarella during his life?

    PREFIX : <https://w3id.org/akn/data/undo/>
    PREFIX allot: <https://w3id.org/akn/ontology/allot/>
    PREFIX undo: <https://w3id.org/akn/ontology/undo/>
    PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
    PREFIX tvc: <http://www.essepuntato.it/2012/04/tvc/>
    
    SELECT DISTINCT ?role ?context ?start ?end
    WHERE {
        :sergio-mattarella tvc:hasValue ?value .
        
        ?value a undo:ValueInTimeAndContext ; 
            tvc:withValue ?role ;
            tvc:withinContext ?context ;
            tvc:atTime ?time .
        
        ?time a undo:Interval ;
            ti:hasIntervalStartDate ?start .
         
        OPTIONAL {
            ?time ti:hasIntervalEndDate ?end
        }
        
        ?role a allot:Role .
    }


## Question 3

How many times an agent has been assigned to a specific role?

    PREFIX allot: <https://w3id.org/akn/ontology/allot/>
    PREFIX undo: <https://w3id.org/akn/ontology/undo/>
    PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
    PREFIX tvc: <http://www.essepuntato.it/2012/04/tvc/>
    
    SELECT DISTINCT ?agent ?role ?context (count(?agent) AS ?count)
    WHERE {
        ?agent a undo:Agent ;
            tvc:hasValue ?value .
        
        ?value a undo:ValueInTimeAndContext ; 
            tvc:withValue ?role ;
            tvc:withinContext ?context .
        
        ?role a allot:Role .
    }
    GROUP BY ?agent ?role ?context


## Question 4

Which roles are involved in particular contexts?

    PREFIX allot: <https://w3id.org/akn/ontology/allot/>
    PREFIX undo: <https://w3id.org/akn/ontology/undo/>
    PREFIX tvc: <http://www.essepuntato.it/2012/04/tvc/>
    
    SELECT DISTINCT ?context ?role
    WHERE {
        ?value a undo:ValueInTimeAndContext ;
            tvc:withValue ?role ;
            tvc:withinContext ?context .
            
        ?role a allot:Role .
    }
    

## Question 5

What are all the statuses that have been associated to all the documents?

    PREFIX allot: <https://w3id.org/akn/ontology/allot/>
    PREFIX undo: <https://w3id.org/akn/ontology/undo/>
    PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
    PREFIX tvc: <http://www.essepuntato.it/2012/04/tvc/>
    
    SELECT DISTINCT ?document ?status ?start ?end
    WHERE {
        ?document a undo:Document ;
            tvc:hasValue ?value .
        
        ?value a undo:ValueInTimeAndContext ;
            tvc:withValue ?status ;
            tvc:atTime ?time .
        
        ?time a undo:Interval ;
            ti:hasIntervalStartDate ?start .
        
        OPTIONAL {
            ?time ti:hasIntervalEndDate ?end
        }
            
        ?status a undo:Status .
    }
    